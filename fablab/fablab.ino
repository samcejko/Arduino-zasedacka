#include <GxEPD2_BW.h>
#include <Adafruit_GFX.h>
#include <WiFiS3.h>
#include <ArduinoJson.h>

struct WifiInfo {
  const char* ssid;
  const char* pass;
};

WifiInfo networks[] = {
  {"YOUR_SSID_1", "YOUR_PASSWORD_1"},
  {"YOUR_SSID_2", "YOUR_PASSWORD_2"},
  {"YOUR_SSID_3", "YOUR_PASSWORD_3"}
};
// TODO: Fill in your WiFi networks and passwords above

const int networksCount = sizeof(networks) / sizeof(networks[0]);
const char* server = "your-server-domain.com"; // TODO: Change to your server domain (e.g., 192.168.1.100 or mywebsite.com) 
const char* cesta = "api.php"; 
const int port = 80; 

const int buzzerPin = 12;
#define ARRAY_LEN(array) (sizeof(array) / sizeof(array[0]))
#define Cb5 554
#define Cb4 277
#define Fb4 370
#define Gb4 415

const int midi1[5][3] = {
 {Cb4, 1500, 273}, 
 {Cb4, 136, 136},
 {Fb4, 136, 136},
 {Cb4, 136, 0},
 {Gb4, 136, 0}
};

GxEPD2_BW<GxEPD2_420_GDEY042T81, GxEPD2_420_GDEY042T81::HEIGHT / 4> display(GxEPD2_420_GDEY042T81(10, 9, 8, 7));

const long CHECK_INTERVAL = 5000;
const int FULL_REFRESH_CYCLES = 120;
const int WIFI_RETRIES = 12;

WiFiClient client;
unsigned long lastCheck = 0;

String old_stav = "";
String old_akce = "";
String old_organizator = "";
String old_do_kdy = "";
String old_cas = "";
String old_dalsi_kdo = "";
String old_dalsi_start = "";

int refreshCounter = 0;
bool wasConnected = false;
const unsigned char icon_label[] PROGMEM = { 0x00, 0x00, 0x0F, 0xE0, 0x18, 0x30, 0x30, 0x18, 0x63, 0x8C, 0x46, 0x46, 0x8C, 0x62, 0x88, 0x22, 0x88, 0x22, 0x8C, 0x62, 0x46, 0x46, 0x63, 0x8C, 0x30, 0x18, 0x18, 0x30, 0x0F, 0xE0, 0x00, 0x00 };
const unsigned char icon_user[] PROGMEM = { 0x01, 0x80, 0x03, 0xC0, 0x07, 0xE0, 0x07, 0xE0, 0x03, 0xC0, 0x03, 0xC0, 0x00, 0x00, 0x08, 0x10, 0x1C, 0x38, 0x36, 0x6C, 0x63, 0xC6, 0x41, 0x82, 0x41, 0x82, 0xC1, 0x83, 0x80, 0x01, 0x80, 0x01 };
const unsigned char icon_clock[] PROGMEM = { 0x07, 0xE0, 0x18, 0x18, 0x20, 0x04, 0x40, 0x02, 0x40, 0x02, 0x80, 0x01, 0x80, 0x01, 0x80, 0x3D, 0x80, 0x01, 0x80, 0x01, 0x40, 0x02, 0x40, 0x02, 0x20, 0x04, 0x18, 0x18, 0x07, 0xE0, 0x00, 0x00 };


// TODO: Replace with your own logo bitmap (generate using image2cpp or similar tools)
const unsigned char logo_bitmap[] PROGMEM = {
	0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 
	0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 
	0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 
	0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 
	0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xf8, 0x00, 0x07, 0xff, 0xff, 0xff, 0xff, 0xff, 
	0xff, 0xf8, 0x00, 0x1f, 0xf8, 0x00, 0x07, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xf8, 0x00, 0x1f, 
	0xf8, 0x00, 0x07, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xf8, 0x00, 0x1f, 0xf8, 0x00, 0x0f, 0xff, 
	0xff, 0xff, 0xff, 0xff, 0xff, 0xf8, 0x00, 0x1f, 0xf8, 0x7f, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 
	0xff, 0xff, 0xfe, 0x1f, 0xf8, 0x7f, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xfe, 0x1f, 
	0xf8, 0x7f, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xfe, 0x1f, 0xf8, 0x7f, 0xff, 0xff, 
	0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xfe, 0x1f, 0xf8, 0x3f, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 
	0xff, 0xff, 0xfe, 0x1f, 0xf8, 0x00, 0x1f, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xfe, 0x1f, 
	0xf8, 0x00, 0x1f, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xfe, 0x1f, 0xf8, 0x00, 0x1f, 0xff, 
	0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xfe, 0x1f, 0xf8, 0x3f, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 
	0xff, 0xff, 0xfe, 0x1f, 0xf8, 0x7f, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xfe, 0x1f, 
	0xf8, 0x7f, 0xff, 0xff, 0x9f, 0xff, 0xff, 0xff, 0xff, 0xff, 0xfe, 0x1f, 0xf8, 0x7f, 0xff, 0xfe, 
	0x07, 0xff, 0xfe, 0x00, 0x07, 0xff, 0xfe, 0x1f, 0xf8, 0x7f, 0xff, 0xfe, 0x07, 0xff, 0xfe, 0x00, 
	0x03, 0xff, 0xfe, 0x1f, 0xf8, 0x7f, 0xff, 0xfc, 0x07, 0xff, 0xfe, 0x00, 0x03, 0xff, 0xfe, 0x1f, 
	0xf8, 0x7f, 0xff, 0xfc, 0x03, 0xff, 0xfe, 0x3f, 0x81, 0xff, 0xfe, 0x1f, 0xf8, 0x7f, 0xff, 0xfc, 
	0x03, 0xff, 0xfe, 0x3f, 0xc1, 0xff, 0xfe, 0x1f, 0xf8, 0x7f, 0xff, 0xf8, 0x41, 0xff, 0xfe, 0x3f, 
	0xc1, 0xff, 0xff, 0x1f, 0xff, 0xff, 0xff, 0xf8, 0x61, 0xff, 0xfe, 0x3f, 0xc1, 0xff, 0xff, 0xff, 
	0xff, 0xff, 0xff, 0xf0, 0x60, 0xff, 0xfe, 0x3f, 0x83, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xf0, 
	0xf0, 0xff, 0xfe, 0x00, 0x07, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xe0, 0xf0, 0xff, 0xfe, 0x00, 
	0x0f, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xe1, 0xf0, 0x7f, 0xfe, 0x00, 0x03, 0xff, 0xff, 0xff, 
	0xff, 0xff, 0xff, 0xe1, 0xf8, 0x7f, 0xfe, 0x00, 0x01, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xc0, 
	0x00, 0x3f, 0xfe, 0x3f, 0xe0, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xc0, 0x00, 0x3f, 0xfe, 0x3f, 
	0xf0, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0x80, 0x00, 0x1f, 0xfe, 0x3f, 0xf0, 0xff, 0xff, 0xff, 
	0xf8, 0x7f, 0xff, 0x80, 0x00, 0x1f, 0xfe, 0x3f, 0xe0, 0xff, 0xfe, 0x1f, 0xf8, 0x7f, 0xff, 0x87, 
	0xfe, 0x1f, 0xfe, 0x1f, 0x80, 0xff, 0xfe, 0x1f, 0xf8, 0x7f, 0xff, 0x0f, 0xfe, 0x0f, 0xfe, 0x00, 
	0x01, 0xff, 0xfe, 0x1f, 0xf8, 0x7f, 0xff, 0x0f, 0xff, 0x0f, 0xfe, 0x00, 0x03, 0xff, 0xfe, 0x1f, 
	0xf8, 0x7f, 0xfe, 0x0f, 0xff, 0x07, 0xfe, 0x00, 0x07, 0xff, 0xfe, 0x1f, 0xf8, 0x7f, 0xff, 0xff, 
	0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xfe, 0x1f, 0xf8, 0x7f, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 
	0xff, 0xff, 0xfe, 0x1f, 0xf8, 0x7f, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xfe, 0x1f, 
	0xf8, 0x7f, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xfe, 0x1f, 0xf8, 0x7f, 0xff, 0xff, 
	0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xfe, 0x1f, 0xf8, 0x7f, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 
	0xff, 0xff, 0xfe, 0x1f, 0xf8, 0x7f, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xfe, 0x1f, 
	0xf8, 0x7f, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xfe, 0x1f, 0xf8, 0x7f, 0xff, 0xff, 
	0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xfe, 0x1f, 0xf8, 0x7f, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 
	0xff, 0xff, 0xfe, 0x1f, 0xf8, 0x7f, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xfe, 0x1f, 
	0xf8, 0x3f, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xfe, 0x1f, 0xf8, 0x00, 0x0f, 0xff, 
	0xff, 0xff, 0xff, 0xff, 0xff, 0xf8, 0x00, 0x1f, 0xf8, 0x00, 0x0f, 0xff, 0xff, 0xff, 0xff, 0xff, 
	0xff, 0xf8, 0x00, 0x1f, 0xf8, 0x00, 0x0f, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xf8, 0x00, 0x1f, 
	0xf8, 0x00, 0x1f, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xf8, 0x00, 0x1f, 0xff, 0xff, 0xff, 0xff, 
	0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 
	0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff
};
// TODO: Replace with your own QR code bitmap
const unsigned char qr_bitmap[] PROGMEM = {
  0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xc0, 0x00, 0x1e, 0x3e, 0x70, 0xf8, 0x00, 0x03, 
  0xc0, 0x00, 0x1e, 0x3e, 0x70, 0xf8, 0x00, 0x03, 0xcf, 0xff, 0x19, 0xf0, 0x03, 0x18, 0xff, 0xf3, 
  0xcf, 0xff, 0x19, 0xf0, 0x03, 0x18, 0xff, 0xf3, 0xcf, 0xff, 0x19, 0xf0, 0x03, 0x18, 0xff, 0xf3, 
  0xcc, 0x03, 0x18, 0x00, 0x7c, 0x18, 0xc0, 0x73, 0xcc, 0x03, 0x18, 0x00, 0x7c, 0x18, 0xc0, 0x73, 
  0xcc, 0x03, 0x1e, 0x01, 0x80, 0x18, 0xc0, 0x73, 0xcc, 0x03, 0x1e, 0x01, 0x80, 0x18, 0xc0, 0x73, 
  0xcc, 0x03, 0x1e, 0x01, 0x80, 0x18, 0xc0, 0x73, 0xcc, 0x03, 0x19, 0xff, 0x8c, 0x18, 0xc0, 0x73, 
  0xcc, 0x03, 0x19, 0xff, 0x8c, 0x18, 0xc0, 0x73, 0xcf, 0xff, 0x18, 0x31, 0xff, 0xf8, 0xff, 0xf3, 
  0xcf, 0xff, 0x18, 0x31, 0xff, 0xf8, 0xff, 0xf3, 0xc0, 0x00, 0x19, 0xce, 0x73, 0x18, 0x00, 0x03, 
  0xc0, 0x00, 0x19, 0xce, 0x73, 0x18, 0x00, 0x03, 0xc0, 0x00, 0x19, 0xce, 0x73, 0x18, 0x00, 0x03, 
  0xff, 0xff, 0xf8, 0x3e, 0x73, 0xff, 0xff, 0xff, 0xff, 0xff, 0xf8, 0x3e, 0x73, 0xff, 0xff, 0xff, 
  0xc3, 0x9f, 0x07, 0xce, 0x73, 0xf8, 0x06, 0x0f, 0xc3, 0x9f, 0x07, 0xce, 0x73, 0xf8, 0x06, 0x0f, 
  0xc3, 0x9f, 0x07, 0xce, 0x73, 0xf8, 0x06, 0x0f, 0xc0, 0x7c, 0xe7, 0xc1, 0x83, 0xf8, 0x3f, 0x8f, 
  0xc0, 0x7c, 0xe7, 0xc1, 0x83, 0xf8, 0x3f, 0x8f, 0xcf, 0xe0, 0x1e, 0x0e, 0x70, 0xe0, 0xf8, 0x7f, 
  0xcf, 0xe0, 0x1e, 0x0e, 0x70, 0xe0, 0xf8, 0x7f, 0xf3, 0x9c, 0xf9, 0xff, 0xf3, 0x18, 0xc7, 0xf3, 
  0xf3, 0x9c, 0xf9, 0xff, 0xf3, 0x18, 0xc7, 0xf3, 0xf3, 0x9c, 0xf9, 0xff, 0xf3, 0x18, 0xc7, 0xf3, 
  0xff, 0xe0, 0x07, 0xfe, 0x0f, 0x18, 0x3e, 0x03, 0xff, 0xe0, 0x07, 0xfe, 0x0f, 0x18, 0x3e, 0x03, 
  0xff, 0x80, 0xe0, 0x0f, 0xf3, 0x1f, 0x06, 0x7f, 0xff, 0x80, 0xe0, 0x0f, 0xf3, 0x1f, 0x06, 0x7f, 
  0xff, 0x80, 0xe0, 0x0f, 0xf3, 0x1f, 0x06, 0x7f, 0xcf, 0xfc, 0x06, 0x00, 0x00, 0x18, 0xc1, 0x8f, 
  0xcf, 0xfc, 0x06, 0x00, 0x00, 0x18, 0xc1, 0x8f, 0xf0, 0x7c, 0xf8, 0x0e, 0x0f, 0x00, 0x3f, 0xff, 
  0xf0, 0x7c, 0xf8, 0x0e, 0x0f, 0x00, 0x3f, 0xff, 0xf0, 0x7c, 0xf8, 0x0e, 0x0f, 0x00, 0x3f, 0xff, 
  0xc0, 0x63, 0x1f, 0xcf, 0xff, 0x00, 0x07, 0xf3, 0xc0, 0x63, 0x1f, 0xcf, 0xff, 0x00, 0x07, 0xf3, 
  0xff, 0xff, 0xf8, 0x3f, 0x83, 0x1f, 0xc7, 0xff, 0xff, 0xff, 0xf8, 0x3f, 0x83, 0x1f, 0xc7, 0xff, 
  0xff, 0xff, 0xf8, 0x3f, 0x83, 0x1f, 0xc7, 0xff, 0xc0, 0x00, 0x19, 0xce, 0x0f, 0x18, 0xc1, 0x83, 
  0xc0, 0x00, 0x19, 0xce, 0x0f, 0x18, 0xc1, 0x83, 0xcf, 0xff, 0x1e, 0x3e, 0x7f, 0x1f, 0xc0, 0x03, 
  0xcf, 0xff, 0x1e, 0x3e, 0x7f, 0x1f, 0xc0, 0x03, 0xcc, 0x03, 0x1f, 0xc0, 0x7f, 0x00, 0x06, 0x0f, 
  0xcc, 0x03, 0x1f, 0xc0, 0x7f, 0x00, 0x06, 0x0f, 0xcc, 0x03, 0x1f, 0xc0, 0x7f, 0x00, 0x06, 0x0f, 
  0xcc, 0x03, 0x18, 0x30, 0x00, 0x18, 0xc1, 0xff, 0xcc, 0x03, 0x18, 0x30, 0x00, 0x18, 0xc1, 0xff, 
  0xcc, 0x03, 0x1e, 0x30, 0x7c, 0xe0, 0xf8, 0x73, 0xcc, 0x03, 0x1e, 0x30, 0x7c, 0xe0, 0xf8, 0x73, 
  0xcc, 0x03, 0x1e, 0x30, 0x7c, 0xe0, 0xf8, 0x73, 0xcf, 0xff, 0x18, 0x30, 0x03, 0x07, 0x38, 0x03, 
  0xcf, 0xff, 0x18, 0x30, 0x03, 0x07, 0x38, 0x03, 0xc0, 0x00, 0x18, 0x3e, 0x03, 0x1f, 0xc7, 0x83, 
  0xc0, 0x00, 0x18, 0x3e, 0x03, 0x1f, 0xc7, 0x83, 0xc0, 0x00, 0x38, 0x3e, 0x03, 0x1f, 0xc7, 0x83, 
  0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff
};

void playMidi(int pin, const int notes[][3], size_t len){
 for (int i = 0; i < len; i++) {
    tone(pin, notes[i][0]);
    delay(notes[i][1]);
    noTone(pin);
    delay(notes[i][2]);
  }
}

void drawBitmapInverted(int16_t x, int16_t y, const uint8_t *bitmap, int16_t w, int16_t h, uint16_t color) {
  int16_t byteWidth = (w + 7) / 8;
  uint8_t byte = 0;
  for (int16_t j = 0; j < h; j++, y++) {
    for (int16_t i = 0; i < w; i++) {
      if (i & 7) byte <<= 1;
      else byte = ~pgm_read_byte(&bitmap[j * byteWidth + i / 8]); 
      if (byte & 0x80) display.drawPixel(x + i, y, color);
    }
  }
}

void connectToSmartWiFi() {
  if (WiFi.status() == WL_CONNECTED) return;
  int numNetworks = WiFi.scanNetworks();
  if (numNetworks == -1) return;

  int bestIndex = -1;
  long bestRSSI = -1000;

  for (int i = 0; i < numNetworks; i++) {
    String foundSSID = WiFi.SSID(i);
    long currentRSSI = WiFi.RSSI(i);
    for (int j = 0; j < networksCount; j++) {
      if (foundSSID == String(networks[j].ssid)) {
        if (currentRSSI > bestRSSI) {
          bestRSSI = currentRSSI;
          bestIndex = j;
        }
      }
    }
  }

  if (bestIndex != -1) {
    Serial.println(networks[bestIndex].ssid);
    WiFi.begin(networks[bestIndex].ssid, networks[bestIndex].pass);
    
    for (int retries = 0; retries < WIFI_RETRIES && WiFi.status() != WL_CONNECTED; retries++) {
      delay(500);
    }
  }
}

void prekresliDisplej(String stav, String akce, String organizator, String do_kdy, String cas, String dalsi_kdo, String dalsi_start, bool fullUpdate);

void setup() {
  Serial.begin(115200);
  delay(500);
  pinMode(buzzerPin, OUTPUT);
  display.init(115200, true, 2, false);
  display.setRotation(0);
  display.setTextColor(GxEPD_BLACK);
  display.setFullWindow();
  display.firstPage();
  do {
    display.fillScreen(GxEPD_WHITE);
    display.setCursor(100, 140);
    display.setTextSize(2);
    display.print("Hledam WiFi...");
  } while (display.nextPage());

  connectToSmartWiFi();
  if (WiFi.status() == WL_CONNECTED) {
    wasConnected = true;
    playMidi(buzzerPin, midi1, ARRAY_LEN(midi1));
    stahniData();
  }
}

void loop() {
  if (millis() - lastCheck >= CHECK_INTERVAL) {
    bool connected = (WiFi.status() == WL_CONNECTED);
    if (!connected) {
      if (wasConnected) {
        prekresliDisplej(old_stav, old_akce, old_organizator, old_do_kdy, old_cas, old_dalsi_kdo, old_dalsi_start, false);
        wasConnected = false;
      }
      WiFi.disconnect();
      connectToSmartWiFi();
      connected = (WiFi.status() == WL_CONNECTED);
    }
    if (connected) {
      wasConnected = true;
      stahniData();
    }
    lastCheck = millis();
  }
}

void stahniData() {
  if (client.connect(server, port)) {
    client.print(String("GET ") + cesta + " HTTP/1.1\r\n" +
                 "Host: " + server + "\r\n" +
                 "Connection: close\r\n\r\n");

    unsigned long timeout = millis();
    while (client.available() == 0) {
      if (millis() - timeout > 5000) { client.stop(); Serial.println("Timeout"); return; }
    }

    if (client.find("\r\n\r\n")) {
      String jsonObsah = client.readString();
      int start = jsonObsah.indexOf('{');
      int konec = jsonObsah.lastIndexOf('}');
      if (start != -1 && konec != -1) {
        jsonObsah = jsonObsah.substring(start, konec + 1);
        StaticJsonDocument<1024> doc;
        DeserializationError error = deserializeJson(doc, jsonObsah);

        if (!error) {
          Serial.println("OK");
          
          String stav = doc["stav"] | "Neznamy";
          String akce = doc["kdo"] | "";
          String organizator = doc["organizator"] | "";
          String do_kdy = doc["do_kdy"] | "";
          String cas = doc["cas_ted"] | "--:--";
          String dalsi_kdo = doc["dalsi_kdo"] | "";
          String dalsi_start = doc["dalsi_start"] | "";

          bool dataChanged = (stav != old_stav || akce != old_akce || organizator != old_organizator || 
                             do_kdy != old_do_kdy || cas != old_cas || dalsi_kdo != old_dalsi_kdo);
          bool needsFullRefresh = (refreshCounter >= FULL_REFRESH_CYCLES);
          
          if (dataChanged || needsFullRefresh) {
            if (stav != old_stav && old_stav != "") tone(buzzerPin, 2500, 100);
            
            bool fullRefresh = (stav != old_stav) || needsFullRefresh;
            prekresliDisplej(stav, akce, organizator, do_kdy, cas, dalsi_kdo, dalsi_start, fullRefresh);
            
            old_stav = stav;
            old_akce = akce;
            old_organizator = organizator;
            old_do_kdy = do_kdy;
            old_cas = cas;
            old_dalsi_kdo = dalsi_kdo;
            old_dalsi_start = dalsi_start;
            
            if (fullRefresh) refreshCounter = 0;
          }
          refreshCounter++;
        } else {
          Serial.println("JSON Error");
        }
      }
    }
    client.stop();
  } else {
    Serial.println("Connect Fail");
  }
}

void drawWifiBars(int x, int y, long rssi, uint16_t color) {
  const int barWidth = 6;
  const int barSpace = 4;
  const int maxBars = 4;
  
  int quality = 0;
  if (WiFi.status() == WL_CONNECTED && rssi != 0) {
    if (rssi > -50) quality = 4;
    else if (rssi > -60) quality = 3;
    else if (rssi > -70) quality = 2;
    else if (rssi > -80) quality = 1;
  }

  for (int i = 0; i < maxBars; i++) {
    int height = (i + 1) * 4;
    if (i < quality) {
      display.fillRect(x + (i * (barWidth + barSpace)), y - height, barWidth, height, color);
    } else {
      display.drawRect(x + (i * (barWidth + barSpace)), y - height, barWidth, height, color);
    }
  }
}

void prekresliDisplej(String stav, String akce, String organizator, String do_kdy, String cas, String dalsi_kdo, String dalsi_start, bool fullUpdate) {
  uint16_t bgColor = GxEPD_WHITE; 
  uint16_t fgColor = GxEPD_BLACK;
  if (stav == "OBSAZENO") { bgColor = GxEPD_BLACK; fgColor = GxEPD_WHITE; }

  if (fullUpdate) display.setFullWindow();
  else display.setPartialWindow(0, 0, 400, 300);

  display.firstPage();
  do {
    display.fillScreen(bgColor);
    display.fillRect(0, 0, 400, 50, fgColor);
    
    display.setTextColor(bgColor);
    display.setTextSize(2);
    display.setCursor(10, 15);
    display.print("ZASEDACKA");
    
    long rssi = WiFi.RSSI();
    drawWifiBars(180, 35, rssi, bgColor); 

    display.setCursor(230, 15);
    display.print(cas);

    display.setTextColor(fgColor); 
    display.setTextSize(4);
    display.setCursor(20, 80); 
    display.print(stav);
    display.fillRect(20, 125, 360, 2, fgColor);
    display.setTextSize(2);
    
    if (stav == "OBSAZENO") {
        display.drawBitmap(20, 130, icon_user, 16, 16, fgColor);
        display.setCursor(45, 130);
        display.print(organizator != "" ? organizator : "-"); 

        display.drawBitmap(20, 160, icon_label, 16, 16, fgColor);
        display.setCursor(45, 160);
        display.print(akce);
        
        display.drawBitmap(20, 190, icon_clock, 16, 16, fgColor);
        display.setCursor(45, 190);
        display.print(do_kdy);
    } else {
        if (dalsi_kdo != "") {
            display.setCursor(20, 150);
            display.print("Dalsi: " + dalsi_start);
            display.setCursor(20, 180);
            display.print(dalsi_kdo);
        } else {
            display.setCursor(20, 150);
            display.print("Zadne dalsi plany");
        }
    }
    drawBitmapInverted(220, 230, qr_bitmap, 64, 64, fgColor);
    drawBitmapInverted(295, 230, logo_bitmap, 96, 60, fgColor);

  } while (display.nextPage());
  
  if (!fullUpdate) display.powerOff(); 
}